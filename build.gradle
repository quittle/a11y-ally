import org.gradle.util.VersionNumber

buildscript {
    ext {
        KOTLIN_VERSION = '1.6.21'
        ANDROID_GRADLE_VERSION = '4.1.3'
        JUNIT_VERSION = '5.8.2'
        QUALITY_TOOLS_VERSION = '0.21.0'

        IS_IN_CI = 'true' == System.getenv('CI')
        IS_CI_RELEASE = 'true' == System.getenv('IS_CI_RELEASE')
    }

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
        classpath "com.android.tools.build:gradle:$ANDROID_GRADLE_VERSION"
        classpath "com.vanniktech:gradle-code-quality-tools-plugin:$QUALITY_TOOLS_VERSION"
    }
}

plugins {
    id 'com.vanniktech.code.quality.tools' version "$QUALITY_TOOLS_VERSION"
    id 'com.quittle.setup-android-sdk' version '3.0.0'
}

allprojects {
    buildscript {
        repositories {
            mavenCentral()
            google()
        }
    }

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    codeQualityTools {
        htmlReports true
        xmlReports false

        detekt {
            config = rootProject.file('config/detekt-config.yml')
        }

        ktlint {
            final String hardcodedVersion = '0.40.0'
            if (VersionNumber.parse(hardcodedVersion) > VersionNumber.parse(toolVersion)) {
                toolVersion = hardcodedVersion
            }
        }

        cpd {
            enabled false
        }
    }
}

task subprojectDefaultTasks {
    subprojects.each { subproject ->
        subproject.afterEvaluate {
            subproject.defaultTasks.each { task ->
                dependsOn subproject.path+":"+task
            }
        }
    }
}

defaultTasks 'subprojectDefaultTasks'
