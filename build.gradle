import org.gradle.util.VersionNumber

buildscript {
    ext {
        KOTLIN_VERSION = '1.4.32'
        ANDROID_GRADLE_VERSION = '4.2.2'
        JUNIT_VERSION = '5.7.2'

        IS_IN_CI = 'true' == System.getenv('CI')
        IS_CI_RELEASE = 'true' == System.getenv('IS_CI_RELEASE')
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
        classpath "com.android.tools.build:gradle:$ANDROID_GRADLE_VERSION"
    }
}

plugins {
    id 'com.vanniktech.code.quality.tools' version '0.20.0'
    id 'com.quittle.setup-android-sdk' version '2.1.0'
}

allprojects {
    buildscript {
        repositories {
            jcenter()
            google()
        }
    }

    repositories {
        google()
        jcenter()
    }

    codeQualityTools {
        htmlReports true
        xmlReports false

        detekt {
            config = rootProject.file('config/detekt-config.yml')
        }

        ktlint {
            final String hardcodedVersion = '0.40.0'
            if (VersionNumber.parse(hardcodedVersion) > VersionNumber.parse(toolVersion)) {
                toolVersion = hardcodedVersion
            }
        }

        cpd {
            enabled false
        }
    }
}

task subprojectDefaultTasks {
    subprojects.each { subproject ->
        subproject.afterEvaluate {
            subproject.defaultTasks.each { task ->
                dependsOn subproject.path+":"+task
            }
        }
    }
}

defaultTasks 'subprojectDefaultTasks'
